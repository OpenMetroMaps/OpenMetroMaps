wrapper {
    gradleVersion = '8.3'
}

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
        maven { url 'https://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'org.docstr.gwt:gwt-gradle-plugin:2.1.6'
        classpath('de.topobyte:gradle-dependency-info-plugin:0.0.3');
        classpath('de.topobyte:gradle-dependency-unpack-plugin:0.0.3');
    }
}

repositories {
    maven {
        url 'https://mvn.topobyte.de'
    }
    maven {
        url 'https://mvn.slimjars.com'
    }
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'org.docstr.gwt'
apply plugin: 'eclipse'
apply plugin: 'de.topobyte.dependency-info-gradle-plugin'
apply plugin: 'de.topobyte.dependency-unpack-gradle-plugin'

task allDeps(type: DependencyReportTask) {}

gwt {
    gwtVersion = '2.12.1'

    modules = ['org.openmetromaps.maps.gwt.Simple',
               'org.openmetromaps.maps.gwt.ScrollableSimple',
               'org.openmetromaps.maps.gwt.ScrollableAdvanced',
               'org.openmetromaps.maps.gwt.Demo']

    compiler {
        strict = true;
        classMetadata = false;
        // TODO: do we need to re-enable these?
        // draftCompile = true;
        //disableCastChecking = true;
    }
}

// TODO: how can we configure this again?
//compileGwt {
//    minHeapSize = "512M";
//    maxHeapSize = "1024M";
//}

// Use a custom configuration for the main dependencies. We need to be able to reference
// these ones for dependency unpacking. The GWT plugin adds the gwt-user and gwt-dev
// dependencies to the 'implementation' configuration which pull in a large tree of
// transitive dependencies. So when we unpack the 'implementation' dependency set,
// we unpack a whole lot more dependencies than just the few ones were adding ourselves.
configurations {
    gwtdeps
}

dependencies {
    gwtdeps 'de.topobyte:viewports-gwt:0.0.3'
    gwtdeps 'de.topobyte:adt-geo:0.0.4'
    gwtdeps 'de.topobyte:adt-misc:0.0.2'
    gwtdeps 'de.topobyte:jsi-rectangle-intersection-tester:0.0.1'
    gwtdeps 'de.topobyte:lightweight-geometry:1.0.0'
    gwtdeps 'de.topobyte:lightweight-geometry-convex-hull:1.0.0'
    gwtdeps 'de.topobyte:geomath:0.0.2'
    gwtdeps 'de.topobyte:line-printer-core:0.0.1'
    gwtdeps 'de.topobyte:line-printer-slf4j:0.0.1'
    gwtdeps 'de.topobyte:simple-formatting:0.0.3'
    gwtdeps 'de.topobyte:system-utils:0.0.1'
    gwtdeps 'de.topobyte:collections-utils:0.0.5'
    gwtdeps 'com.slimjars.trove4j:trove4j-int-hash-set:1.0.1'
    gwtdeps 'de.topobyte:slf4j-transpilable-binding-jdk:0.0.3'
}

configurations {
    gwtdeps.exclude group: "org.slf4j", module: "slf4j-api"
    gwtdeps.exclude group: "com.vividsolutions", module: "jts"
    gwtdeps.exclude group: "com.slimjars.trove4j", module: "trove4j-object-int-hash-map"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
        }
    }
    main {
        java {
            srcDir 'src/modules/maps-core/java'
            srcDir 'src/modules/maps-gwt/java'
            srcDir 'src/modules/maps-model/java'
            srcDir 'src/modules/maps-model-xml/java'
            srcDir 'src/modules/maps-model-xml-converter/java'
            srcDir 'src/modules/maps-model-util/java'
            srcDir 'src/modules/line-network-util/java'
            srcDir 'src/modules/lightgeom/java'
            srcDir 'src/modules/xml-dom-abstraction/java'
            srcDir 'src/modules/xml-dom-abstraction-gwt/java'
        }
    }
}

depinfo {
    configuration "gwtdeps"
}

depunpack {
    configuration "gwtdeps"
}

task depfiles {
    doLast {
        project.configurations.gwtdeps.each { println it.name }
    }
}

task depcoords {
    doLast {
        project.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
            ComponentIdentifier ci = it.artifactId.componentIdentifier
            if (ci instanceof ModuleComponentIdentifier) {
                ModuleComponentIdentifier mci = ci
                println String.format("%s:%s:%s", mci.group, mci.module, mci.version)
            }
        }
    }
}
